buildscript {
    ext {
        grpcVersion = '1.66.0'
        protobufPluginVersion = '0.9.4'
        protobufVersion = '3.21.12'
        grpcSpringBootVersion = '5.1.5'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2' // Spring Boot 플러그인
    id 'io.spring.dependency-management' version '1.1.6' // 의존성 관리 플러그인
    id 'groovy' // Groovy 지원 (선택사항)
    id 'idea' // IntelliJ IDEA 지원
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}

group = 'com.grpc.server'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Grpc
    implementation 'net.devh:grpc-spring-boot-starter:3.1.0.RELEASE'
    implementation 'net.devh:grpc-client-spring-boot-starter:3.1.0.RELEASE'
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"

    // grpc 프로토콜 Protobuf 메시지 직렬화/역직렬화
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.mapstruct:mapstruct:1.6.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    //generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

def querydslGeneratedDir = file("build/generated/querydsl")
def mapstructGeneratedDir = file("build/generated/mapstruct")

sourceSets {
    main {
        java {
            srcDir querydslGeneratedDir
            srcDir mapstructGeneratedDir
        }
    }
}

tasks.named('compileJava') {
    options.annotationProcessorGeneratedSourcesDirectory = file("build/generated/sources/annotationProcessor/java/main")
    options.compilerArgs += [
            "-Amapstruct.defaultComponentModel=spring"
    ]
}

tasks.named('test') {
    useJUnitPlatform()
}
